import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdk 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    defaultConfig {
        minSdk 24
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their MD5 checksums.
        def filesToDownload = [
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.9/default-arm64-v8a.jar", "md5": "9b385138435ce89c23679a8c55356795", "destination": file("$buildDir/v1.1.9/default-arm64-v8a.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.9/default-armeabi-v7a.jar", "md5": "bf219bd706a5d954808ae4533cad49d8", "destination": file("$buildDir/v1.1.9/default-armeabi-v7a.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.9/default-x86_64.jar", "md5": "2cecc7f316e183effaec89be3ce332e8", "destination": file("$buildDir/v1.1.9/default-x86_64.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-video-build/releases/download/v1.1.9/default-x86.jar", "md5": "04f8ce44c477995c579009f0ba34ad9c", "destination": file("$buildDir/v1.1.9/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedMD5 != fileInfo.md5) {
                    destFile.delete()
                    println "MD5 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedMD5 != fileInfo.md5) {
                    throw new GradleException("MD5 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)